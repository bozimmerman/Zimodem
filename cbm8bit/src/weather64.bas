!--------------------------------------------------
!- Sunday, May 14, 2017 10:01:15 PM
!- Import of : 
!- c:\src\zimodem\cbm8bit\weather.prg
!- Commodore 64
!--------------------------------------------------
0 CLR:
2 REM 140 DOUGHERTY AVE
3 REM HOLBROOK,N.Y.
4 :
5 REM CURSOR #1, JUNE-JULY 1984
6 REM ADAPTED TO C64 IN APR, 2017
7 REM BY BO ZIMMERMAN
9 X=PEEK(186):POKE254,X:IFX<8THENPOKE254,8
10 OPEN5,2,0,CHR$(8):DIMPP$(25):P$="ok":POKE665,73:POKE666,3:POKE186,PEEK(254)
11 ML=49152:TR=0:IFPEEK(ML)<>76THENCLOSE5:LOAD"pml64.bin",PEEK(186),1:RUN
12 MV=ML+18:GET#5,A$:IFA$<>""THEN12
13 PRINT#5,"athz0&p0e0r0":INPUT#5,P$:IFP$<>"ok"THENTR=TR+1:IFTR<8THEN12
14 PRINT#5,"atn0v1x1f3q0s40=250";CHR$(19):INPUT#5,P$:IFP$="ok"THEN20
15 TR=TR+1:ONTRGOTO14,14,14,14,14,14,14,14,14,17
17 PRINT"no modem detected?!":CLOSE5:STOP
20 PRINT"c64net wifi modem initialized"
90 PG$="weather!":NM$="1":GOSUB62000
100 READAA,BB:FORZ=AATOBB:READW:POKEZ,W:NEXT
105 PRINT"{down}what's your name? ";:GOSUB60000:EP$=IN$:IFEP$=""THEN105
106 P$="name: "+EP$:GOSUB3000:P$="name: ":GOSUB2200:WP$=P$
107 IFOA=1THENWP$=EP$:EP$=P$
110 DATA826,849,162,4  ,160,0,132,33,134,34,177,33,73,128,145,33
120 DATA200,208,247,232,224,8  ,208,240,96,0
130 Q=54272+4:R=54272:S=54272+1:POKER,195:POKES,16:H=1786
135 POKE54272+5,0:POKE54272+6,240:POKE54272+24,14
140 A$="{home}{down*20}"
145 BL$="                                      "
150 C$="{cm m}{left}{down}N{left}{down}M{down}M{left}{down}NM{cm @}{left*3}{down}M  M{left*4}{down}{cm m}{left}{down}N{left*2}{down}N{left}{down}"
160 D$=" {left}{down} {left}{down} {down} {left}{down}   {left*3}{down}    {left*4}{down} {left}{down} {left*2}{down} {left}{down}"
170 E$=C$+"M{left}{down}N{left*2}{down}N{left}{down}{cm g}{left*2}{down}N{left*4}{down} {cm @}N {left*4}{down}N "
180 F$=D$+" {left}{down} {left*2}{down} {left}{down} {left*2}{down} {left*3}{down}  {left*3}{down} "
190 G$=C$+"M{down}{cm g}{left}{down}{cm g}{left}{down}M{down}M{left}{down} M{cm @} {left*2}{down} M"
200 H$=D$+" {down} {left}{down} {left}{down} {down} {down}  {down} "
210 I$=C$+"M{down}M{down}{cm g}{left*2}{down} {cm g}{left*3}{down} N {left*4}{down} N {left*3}{down} {cm g}"
220 J$=D$+" {down} {down} {left}{down} {left*2}{down} {left*2}{down} {left}{down} "
230 PRINT"{clear}{cm a}";:FORV=1TO38:PRINT"{sh asterisk}";:NEXT:PRINT"{cm s}";
235 F=28:G=28:M=3:MM=0
240 FORV=1TO19:PRINT"{sh -}";BL$;"{sh -}";:NEXT
250 PRINT"{up*4}{right*3}{reverse on}{sh pound}M    {cm asterisk}"SPC(20)"{sh pound}    N{cm asterisk}"
255 PRINT"{up}{right*3}{reverse on} {cm m}{cm t*5}"SPC(20)"{cm t*5}{cm g} "
260 PRINT"{right*3}{reverse on} {cm m}     "SPC(20)"     {cm g} "
265 T$=LEFT$(EP$,7):PRINT"{up*2}{reverse on}";SPC(3.5+(7-LEN(T$))/2);T$
275 T$=LEFT$(WP$,7):PRINT"{up*2}{reverse on}";SPC(30+(7-LEN(T$))/2);T$
285 PRINT"{up}{right*3}{reverse on}{cm @}{sh @}{cm @*5}"SPC(20)"{cm @*5}L{cm @}"
290 PRINTA$:FORV=1TO40:PRINT"{reverse on}{cm i}";:NEXT:PRINT
295 REME Q,16:POKER,15
300 GOSUB970:IFF<1ORG<1THEN1390
310 M=M+1:IFM>3THENM=1:MM=MM+1:B=0:PRINT"{home}";TAB(15);"{sh asterisk*11}
315 IFM=3THENGOSUB4100:IFRN>.33THEN310
320 PRINT"{home}{down}{reverse off}";:FORV=1TO3:PRINT"{sh -}";BL$;"{sh -}";:NEXTV:PRINT
330 A=1068:GOSUB4000:AA=RN:AA=AA*26:A=A+AA:B=1-B
340 POKEA,104:FORV=1TO4:A=A+1:POKEA,102:NEXT:FORV=1TO5:A=A+1:POKEA,104:NEXT
350 A=A+27:POKEA,104:FORV=1TO11:A=A+1:POKEA,102:NEXT:POKEA,104:A=A+33
360 POKEA,104:FORV=1TO6:A=A+1:POKEA,102:NEXT:POKEA,104:NN=0
365 PRINTA$"{reverse on}";BL$;"  ":PRINTA$;"{right*3}";
370 IFM=3THENPRINT"target"SPC(21)"target":GOTO400
380 IFB=0THENPRINT"target"SPC(21)"{reverse on}attacker":GOTO400
390 PRINT"{reverse on}attacker{reverse off}"SPC(20)"target
400 GOSUB4100:EE=INT(RN*100)
410 IFEE>50THENEE=INT(-EE/2):PRINTA$SPC(13)"{reverse on} wind <CC"ABS(EE)"{left}    {down}":GOTO430
420 PRINTA$SPC(13)"{reverse on} wind CC>"EE"{left}     {down}"
430 PRINTBL$:PRINTBL$;"{up*2}"
450 IFM<3THEN490
460 FORX=1TO4:PRINTBL$;"{up}":GOSUB1280:GOSUB1260
470 PRINTTAB(13)"{reverse on}act of nature{reverse off}{up}":GOSUB1280:NEXT
480 GOSUB1270:GOSUB1270:PRINTBL$
482 GOSUB4100:O=1:B$=MID$("hlrt",INT(RN*4)+1,O):IN$=B$:GOTO520
490 IFM<>1THENGOSUB1280:GOTO505
500 POKEQ,33:PRINT"{home}";TAB(15)" round"MM"{left} ":FORX=1TO2:GOSUB1260:NEXT:POKEQ,32
505 T$=EP$:IFB=0THENT$=WP$
510 PRINTA$"{down*2}";T$;": weapon (h,l,r,t,q) ";:IFB<>OATHEN513
511 PRINT": ";:P$="weapon: ":GOSUB2200:IN$=P$:B$=P$:O=1:PRINT
512 PRINT"{up}";BL$:GOTO520
513 PRINT"? ";:GOSUB60000
515 PRINT"{up}";BL$:B$=LEFT$(IN$,1):O=LEN(IN$):IFO=0THEN510
517 P$="weapon: "+B$:GOSUB3000
520 W$="rain":IFLEFT$(W$,O)=IN$THENFF=5:TT=FF:O=2:GOTO590
530 W$="hail":IFLEFT$(W$,O)=IN$THENCC=58:FF=4:TT=FF:O=0:GOTO590
540 W$="tornado":IFLEFT$(W$,O)=IN$THENCC=102:FF=7:TT=FF:O=1:GOTO590
550 W$="lightning":IFLEFT$(W$,O)=IN$THENO=0:GOTO590
560 W$="quit":IFLEFT$(W$,O)=IN$THEN1390
580 PRINT"{up*2}":GOTO430
590 PRINT"{up} weapon is {reverse on}";W$
595 IFO=1THENPRINT"{home}{down}";:FORV=1TO40:PRINT"{cm +*2}";:NEXT:PRINTA$;"{down*2}
600 IFM=3THENA1=0:PRINT
605 IFB<>OATHEN610
606 IFM<3THENP$="charge: ":GOSUB2200:IN$=P$:BB$=P$:A1=VAL(BB$):PRINT:GOTO615
610 IFM<3THENPRINT"{right}charge? ";:GOSUB60000:BB$=IN$:A1=VAL(BB$)
612 IFM<3ANDA1>=0THENP$="charge: "+MID$(STR$(A1),2):GOSUB3000
613 IFM<3ANDA1<0THENP$="charge: "+STR$(A1):GOSUB3000
615 PRINT"{up} charge is";A1
620 IFA1<-150THENA1=-150
630 IFA1>150THENA1=150
640 IFB$="h"THENGOSUB1270:GOTO670
650 IFB$="l"THENGOSUB1270:GOTO1070
660 IFO=1THENPRINT"{home}{down*3}";:FORV=1TO20:PRINT"{cm +*2}";:NEXT:PRINT:EE=1.5*EE
670 E=(A1+EE)/50:DD=0:GG=104
680 IFO=2ANDE<0THENCC=47:GOTO710
690 IFO=2ANDE>0THENCC=77:GOTO710
700 IFO=2THENCC=66
710 A=1148+AA:BB=0:FF=TT:T=WW:I=0:FORW=1TO16:WW=W:POKER,W*15:POKES,30+(3*W)
715 POKEQ,17
720 IFO<>1ORDD=1THENPOKEQ,16
730 IFT=WWTHENW=16:WW=0
740 A=A+(40+E):GOSUB870:NEXT
750 IFO<>1THEN840
760 IFPEEK(C+1)=93ORPEEK(C-1)=93THEN840
770 POKEC+1,GG:POKEC-1,GG
780 IFPEEK(C+2)=93ORPEEK(C-2)=93THEN840
790 POKEC+2,GG:POKEC-2,GG
800 IFE>0ORE=0THENPOKEC-38,32:POKEC-39,32
810 IFE<0ORE=0THENPOKEC-41,32:POKEC-42,32
820 IFDD=0THENGOSUB1370:GOTO840
825 IFDD=1THENPOKEQ,17
830 IFDD=1THENPOKER,51:FORV=100TO10STEP-2:POKES,V:NEXT:POKER,15:POKEQ,16
840 IFNN<>1THENGOSUB1270
850 IFDD=0THENDD=1:CC=32:GG=CC:GOTO710
860 GOTO300
865 GOTO 300
870 C=A:FORZ=1TOFF:C=C+1:D=PEEK(C):IFO=1ANDE<0THEND=PEEK(C-1)
880 IFD=93THENGOSUB1310:W=16:O=0:RETURN
890 IFD>127ANDDD=0THENGOSUB940
900 IFI>2THENW=16
910 POKEC,CC:IFCC=58THENPOKEQ,17:POKES,FF*10:POKEQ,16
920 NEXT:BB=1-BB:IFBB=0ANDB$="t"THENFF=FF-1
930 RETURN
940 FORX=1TO3:POKEC,170:GOSUB960:POKEC,58:GOSUB960:NEXT:I=I+1:IFO<>0THENW=16
950 RETURN
960 POKEQ,17:POKES,Z*10:FORV=1TO50:NEXT:POKEQ,16:RETURN
970 F=0:G=-1:P=0
980 FORY=1TO4:FORX=1TO7:H=H+1:D=PEEK(H):IFD<>32THENP=P+1
990 NEXT:H=H-47:NEXT
1000 IFG=-1THENF=P:G=0:P=0:H=H+187:GOTO980
1010 G=P:H=1786
1020 RETURN
1070 A1=A1/33:IFA1<-4THENA1=-4
1080 IFA1>4THENA1=4
1090 IFA1>1THENA1=A1-1
1100 AA=AA+A1+7
1110 IFAA>33THENAA=33
1120 IFAA<6THENAA=6
1130 FORZ=1TO3:GOSUB1250:PRINTC$:SYS826:SYS826:GOSUB1250:PRINTD$:NEXT
1140 GOSUB1260:GOSUB1250:PRINTC$:SYS826:SYS826:GOSUB1250:PRINTD$:GOSUB1270
1150 IFA1>0THEN1200
1160 IFA1=0THEN1230
1170 FORZ=1TO2:GOSUB1250:PRINTE$:SYS826:GOSUB1290:SYS826:GOSUB1250:PRINTF$
1180 AA=AA+1
1190 NEXT:GOTO300
1200 FORZ=1TO2:GOSUB1250:PRINTG$:SYS826:GOSUB1290:SYS826:GOSUB1250:PRINTH$
1210 AA=AA-1
1220 NEXT:GOTO300
1230 FORZ=1TO2:GOSUB1250:PRINTI$:SYS826:GOSUB1290:SYS826:GOSUB1250:PRINTJ$
1240 NEXT:GOTO300
1250 PRINT"{home}{down*4}"SPC(AA);:FORV=1TO25:NEXT:RETURN
1260 FORV=1TO500:NEXT:RETURN
1270 FORV=1TO1000:NEXT:RETURN
1280 POKEQ,17:POKES,M*50:FORV=1TO100:NEXT:POKEQ,16:RETURN
1290 POKEQ,129:FORV=1TO30:NEXT:POKEQ,128:POKER,15
1300 RETURN
1310 IFNN=1THENRETURN
1330 PRINT"{home}";TAB(13);"{reverse on}out of bounds!":GOSUB1370:FORV=1TO2500:NEXT
1350 PRINT"{home}";TAB(13)"{sh asterisk*2} round"MM"{left} {sh asterisk*3}":NN=1:RETURN
1370 FORX=1TO10:FORV=10TO200STEP25:POKES,V:NEXT:NEXT:POKES,0:RETURN
1390 PRINTA$;"{reverse on}  ";BL$:PRINTBL$:PRINTBL$
1400 SYS826:FORX=1TO500:NEXT
1410 FORV=200TO50STEP-1:POKES,V:NEXT:POKES,0:POKER,0:POKEQ,0
1415 SYS826:B$=WP$:P=F*3.6:IFG>0THENB$=EP$:P=G*3.6
1420 T=7-(LEN(EP$)/2):IFT<0THENT=0
1425 PRINT"{up*4}{reverse on}";TAB(T);EP$
1430 T=33.5-LEN(WP$)/2:IFT+LEN(WP$)>39THENT=40-LEN(WP$)
1435 PRINT"{up*2}{reverse on}";TAB(T);WP$
1440 PRINT "{up*8}{right*2}";INT(F*3.6);"{left}% left"
1445 PRINT "{up*2}";TAB(29);INT(G*3.6);"{left}% left"
1450 PRINT"{home}{down*12}{right*10}";
1460 IF F=G THENPRINT"     it's a tie!":GOTO 1500
1470 PRINT"the winner is ";:IF F<G THEN PRINT WP$:GOTO 1500
1480 PRINT EP$
1500 PRINTA$;"{down*2}want to play again? ";:GOSUB60000
1510 IFLEFT$(IN$,1)="n"THEN1600
1520 T$=EP$:EP$=WP$:WP$=T$
1530 GOTO 230
1600 PRINT"{clear}{down*2}":PRINT#5,"atz":FORI=0TO1000:GET#5,A$:NEXTI:CLOSE5:END
2000 GET#5,P$:IFP$<>""THEN2000
2010 PRINT#5,CHR$(17);
2020 SYSML+6:FORI=0TO8STEP2:PR(I)=PEEK(MV+I):NEXTI:PR(1)=PEEK(MV+1)
2030 IFPR(0)>0ANDPR(6)<>PR(8)THENPRINT#5,"atl0":GOTO2020
2040 IFPR(4)=0THENP$=""
2050 IFPR(1)=255THENP$="":PR(2)=0:RETURN2060 IFPR(2)<>CPTHENSTOP
2099 RETURN
2100 GOSUB2000:IFP$=""THEN2100
2110 RETURN
2200 PR$(0)=P$:GOSUB2100:IFPR$(0)=""THENRETURN
2220 IFLEFT$(P$,LEN(PR$(0)))=PR$(0)THENP$=MID$(P$,LEN(PR$(0))+1):RETURN
2230 PRINT"{reverse on}{pink}PROTOCOL FAIL:";PR$(0),P$:PRINT#5,"atz":CLOSE5:STOP
3000 PR$(0)=P$:SYSML+9:PR$(1)=MID$(STR$(PEEK(MV+8)),2)
3010 PRINT#5,"ats42=";PR$(1);"t";CHR$(34);P$;CHR$(34)
3020 SYSML:IFP$<>"ok"ANDP$<>"OK"THENP$=PR$(0):GOTO3000
3030 RETURN
4000 IFOA=0THENRN=RND(TI):P$="rand:"+STR$(RN):GOSUB3000:RETURN
4010 P$="rand: ":GOSUB2200:RN=VAL(P$):RETURN
4100 IFOA=0THENRN=RND(1):P$="rand:"+STR$(RN):GOSUB3000:RETURN
4110 P$="rand: ":GOSUB2200:RN=VAL(P$):RETURN
9999 END
60000 IN$=" ":ZT=TI:ZC=2:ZD$=CHR$(20)
60010 GETZ$:IFZ$<>""THEN60070
60020 IFZT<=TITHENPRINTMID$(" {cm +}",ZC,1);"{left}";:ZC=3-ZC:ZT=TI+15
60030 GOTO60010
60070 Z=ASC(Z$):ZL=LEN(IN$):IF(ZAND127)<32THENPRINT" {left}";:GOTO60110
60080 IFFLAND(ZAND127)>64AND(ZAND127)<91THENZ$=CHR$((Z+128)AND255)
60090 IFZL>254THEN60010
60100 IN$=IN$+Z$:PRINTZ$;ZD$;Z$;
60110 IFZ=13THENIN$=MID$(IN$,2):PRINTCR$;:RETURN
60120 IFZ=20ANDZL>1THENIN$=LEFT$(IN$,ZL-1):PRINT"{left}";:GOTO60010
60130 IFZ=141THENZ$=CHR$(-20*(ZL>1)):FORZ=2TOZL:PRINTZ$;:NEXTZ:GOTO60000
60140 GOTO60010
62000 PRINT"{clear}{down*2}";TAB(9);"cursor #";NM$;"  ";PG$;CHR$(142)
62010 PRINT:PRINTTAB(9);"c64net wifi 1200 baud version"
62020 FORI=1TO10:PRINT"{sh asterisk*4}";:NEXTI
62030 PRINT"{down}take the enemy by storm
62040 PRINT"{down*3}press {reverse on}a{reverse off} to be the host"
62045 PRINT"press {reverse on}o{reverse off} to connect to the remote host"
62050 GETT$:IFT$<>"a"ANDT$<>"o"THEN62050
62060 PRINT"{clear}":CR$=CHR$(13):OA=0:IFT$="a"THENOA=1:GOTO62500
62100 PRINT"enter the host ip: ";:GOSUB60000:IFIN$=""THENCLOSE5:STOP
62110 PRINT"connecting to ";IN$;"...";
62120 PRINT#5,"at&m13&m10&d13c";CHR$(34);IN$;":6502";CHR$(34)
62130 INPUT#5,P$:IFP$="ok"THEN62130
62140 IFLEFT$(P$,8)="connect "THENCP=VAL(MID$(P$,9)):GOTO62150
62145 PRINT"failed.":PRINT:GOTO62100
62150 PRINT"connect!"
62160 P$="protocol: weather1":GOSUB3000
62170 PRINT:PRINT"handshake..."
62180 GOSUB2100
62190 IFP$<>"ready: weather1"THENPRINT"bad resp";:PRINT#5,"atz":CLOSE5:STOP
62200 RETURN
62500 PRINT#5,"ati2"
62510 INPUT#5,P$:IFP$=""ORP$="ok"THEN62500
62520 IP$=P$:IFLEN(P$)<8THEN62500
62530 PRINT#5,"at&m10&m13&d13s41=1s0=1a6502":INPUT#5,P$:IFP$<>"ok"THEN62530
62540 PRINT"your ip address is ";IP$
62550 PRINT"please set your router to forward"
62560 PRINT"port 6502 to the above ip address."
62570 PRINT"then tell your friend to connect."
62580 PRINT"waiting for connection..."
62600 INPUT#5,P$:IFP$=""THEN62600
62610 IFLEFT$(P$,8)<>"connect "THEN62600
62620 PRINT"connect!":CP=VAL(MID$(P$,9))
62630 PRINT#5,"atc";MID$(P$,9):INPUT#5,P$:IFP$<>"ok"ANDP$<>"OK"THEN62630
62650 PRINT:PRINT"handshake..."
62660 GOSUB2100:IFP$<>"protocol: weather1"THENPRINT"bad handshake":CLOSE5:STOP
62670 P$="ready: weather1":GOSUB3000
62680 RETURN
